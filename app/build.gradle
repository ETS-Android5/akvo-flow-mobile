apply plugin: 'com.android.application'
apply plugin: 'io.sentry.android.gradle'
if (!project.hasProperty("nodexcount")) {
    apply plugin: 'com.getkeepsafe.dexcount'
}
apply plugin: 'io.fabric'

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
    google()
}

sentry {
    autoProguardConfig true
    autoUpload true
}

def isCi = "true" == System.getenv("TRAVIS")

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    Properties versionProps = loadProperties('version.properties')
    Properties surveyProps = loadProperties('survey.properties')

    def versionMajor = versionProps['VERSION_MAJOR'].toString()
    def versionMinor = versionProps['VERSION_MINOR'].toString()
    def versionPatch = versionProps['VERSION_PATCH'].toString()
    def versionCodeProperty = versionProps['VERSION_CODE'].toInteger()
    def buildYear = getBuildYear()

    defaultConfig {
        versionCode versionCodeProperty
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        applicationId "org.akvo.flow"
        testApplicationId "org.akvo.flow.tests"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        testHandleProfiling true
        testFunctionalTest true
        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {

        debug {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file('debug-key/debug.keystore')
            storePassword 'android'
        }

        flowRelease {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        buildTypes.each {
            def serverBase = surveyProps['serverBase'].toString()
            def awsBucket = surveyProps['awsBucket'].toString()
            def awsAccessKeyId = surveyProps['awsAccessKeyId'].toString()
            def awsSecretKey = surveyProps['awsSecretKey'].toString()
            def apiKey = surveyProps['apiKey'].toString()

            def instanceUrl = surveyProps['instanceUrl'].toString()
            if ("null" == instanceUrl) {
                instanceUrl = serverBase
            }

            it.buildConfigField "String", "SERVER_BASE", "\"${serverBase}\""
            it.buildConfigField "String", "INSTANCE_URL", "\"${instanceUrl}\""
            it.buildConfigField "String", "AWS_BUCKET", "\"${awsBucket}\""
            it.buildConfigField "String", "AWS_ACCESS_KEY_ID", "\"${awsAccessKeyId}\""
            it.buildConfigField "String", "AWS_SECRET_KEY", "\"${awsSecretKey}\""
            it.buildConfigField "String", "API_KEY", "\"${apiKey}\""
            it.buildConfigField "String", "SIGNING_KEY", "\"\""
            it.buildConfigField "String", "BUILD_YEAR", "\"${buildYear}\""
        }

        release {
            minifyEnabled true
            proguardFile 'proguard-files/proguard-rules.pro'
            proguardFile 'proguard-files/proguard-butterknife.pro'
            proguardFile 'proguard-files/proguard-glide.pro'
            proguardFile 'proguard-files/proguard-google-play-services.pro'
            proguardFile 'proguard-files/proguard-gson.pro'
            proguardFile 'proguard-files/proguard-maps-utils.pro'
            proguardFile 'proguard-files/proguard-prettytime.pro'
            proguardFile 'proguard-files/proguard-rxjava.pro'
            proguardFile 'proguard-files/proguard-sentry.pro'
            proguardFile 'proguard-files/proguard-square-retrofit.pro'
            proguardFile 'proguard-files/proguard-support-v7-appcompat.pro'
            proguardFile getDefaultProguardFile('proguard-android.txt')
            signingConfig signingConfigs.flowRelease
        }
        debug {
            ext.enableCrashlytics = false
            minifyEnabled true
            multiDexEnabled true
            useProguard false
            proguardFile 'proguard-files/proguard-rules.pro'
            proguardFile 'proguard-files/proguard-butterknife.pro'
            proguardFile 'proguard-files/proguard-glide.pro'
            proguardFile 'proguard-files/proguard-google-play-services.pro'
            proguardFile 'proguard-files/proguard-gson.pro'
            proguardFile 'proguard-files/proguard-maps-utils.pro'
            proguardFile 'proguard-files/proguard-prettytime.pro'
            proguardFile 'proguard-files/proguard-rxjava.pro'
            proguardFile 'proguard-files/proguard-sentry.pro'
            proguardFile 'proguard-files/proguard-square-retrofit.pro'
            proguardFile 'proguard-files/proguard-support-v7-appcompat.pro'
            proguardFile getDefaultProguardFile('proguard-android.txt')
            multiDexKeepProguard file('proguard-files/proguard-multidex-rules.pro')
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude '.readme'
    }

    flavorDimensions "default"

    productFlavors {
        flow {
            dimension "default"
        }
        biogas {
            dimension "default"
        }
        cookstoves {
            dimension "default"
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        // set to true to turn off analysis progress reporting by lint
        quiet false
        // if true, stop the gradle build if errors are found
        abortOnError false
        // if true, only report errors
        ignoreWarnings false
        lintConfig file('lint.xml')
        ignore 'InvalidPackage'
    }

    sourceSets {
        biogas.res.srcDirs = ['src/snv/res', 'src/biogas/res']
        cookstoves.res.srcDirs = ['src/snv/res', 'src/cookstoves/res']
    }
}

private Properties loadProperties(String fileName) {
    Properties versionProps = new Properties()
    def versionPropsFile = file(fileName)
    if (versionPropsFile.canRead()) {
        versionProps.load(new FileInputStream(versionPropsFile))
    } else {
        throw new GradleException("Could not read ${fileName}")
    }
    versionProps
}

private static String getBuildYear() {
    new Date().format('yyyy')
}

android.applicationVariants.all { variant ->
    variant.outputs.all { output ->
        outputFileName = file("/flow.apk")
    }
}

dependencies {
    implementation "com.android.support:appcompat-v7:${rootProject.ext.supportVersion}"
    implementation "com.android.support:exifinterface:${rootProject.ext.supportVersion}"
    implementation "com.android.support:recyclerview-v7:${rootProject.ext.supportVersion}"
    implementation "com.android.support:support-annotations:${rootProject.ext.supportVersion}"
    implementation "com.android.support:design:${rootProject.ext.supportVersion}"
    implementation "com.android.support.constraint:constraint-layout:${rootProject.ext.constraintLayoutVersion}"
    implementation "com.google.android.gms:play-services-maps:${rootProject.ext.gmsVersion}"
    implementation "com.google.android.gms:play-services-gcm:${rootProject.ext.gmsVersion}"
    implementation "com.google.maps.android:android-maps-utils:${rootProject.ext.mapUtilsVersion}"
    implementation "org.ocpsoft.prettytime:prettytime:${rootProject.ext.prettyTimeVersion}"
    implementation "com.jakewharton.timber:timber:${rootProject.ext.timberVersion}"
    implementation "com.google.dagger:dagger:${rootProject.ext.daggerVersion}"
    annotationProcessor "com.google.dagger:dagger-compiler:${rootProject.ext.daggerVersion}"
    implementation "com.jakewharton:butterknife:${rootProject.ext.butterKnifeVersion}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${rootProject.ext.butterKnifeVersion}"
    implementation "io.sentry:sentry-android:${rootProject.ext.sentryVersion}"
    implementation "com.github.bumptech.glide:glide:${rootProject.ext.glideVersion}"
    implementation "com.squareup.sqlbrite2:sqlbrite:${rootProject.ext.sqlbriteVersion}"
    implementation "io.reactivex.rxjava2:rxandroid:${rootProject.ext.rxAndroidVersion}"
    implementation "io.reactivex.rxjava2:rxjava:${rootProject.ext.rxJavaVersion}"
    implementation "com.squareup.okhttp3:logging-interceptor:${rootProject.ext.loggingInterceptorVersion}"
    implementation "com.google.code.gson:gson:${rootProject.ext.gsonVersion}"
    implementation("com.crashlytics.sdk.android:crashlytics:${rootProject.ext.crashlyticsVersion}") {
        transitive = true
    }
    if (isCi) {
        debugImplementation "com.squareup.leakcanary:leakcanary-android:${rootProject.ext.leakCanaryVersion}"
        releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:${rootProject.ext.leakCanaryVersion}"
        debugImplementation "com.squareup.leakcanary:leakcanary-support-fragment:${rootProject.ext.leakCanaryVersion}"
    } else {
        implementation "com.squareup.leakcanary:leakcanary-android-no-op:${rootProject.ext.leakCanaryVersion}"
    }

    implementation "com.android.support:multidex:${rootProject.ext.multidexVersion}"
    implementation "com.google.firebase:firebase-analytics:${rootProject.ext.firebaseVersion}"

    testImplementation "junit:junit:${rootProject.ext.jUnitVersion}"
    testImplementation "org.mockito:mockito-core:${rootProject.ext.mockitoCoreVersion}"
    testImplementation "org.powermock:powermock-module-junit4:${rootProject.ext.powerMockVersion}"
    testImplementation "org.powermock:powermock-module-junit4-rule:${rootProject.ext.powerMockVersion}"
    testImplementation "org.powermock:powermock-api-mockito:${rootProject.ext.powerMockVersion}"
    testImplementation "org.powermock:powermock-classloading-xstream:${rootProject.ext.powerMockVersion}"

    androidTestImplementation "com.google.dexmaker:dexmaker:${rootProject.ext.dexMakerVersion}"
    androidTestImplementation "com.google.dexmaker:dexmaker-mockito:${rootProject.ext.dexMakerVersion}"
    androidTestImplementation("com.android.support.test:rules:${rootProject.ext.supportTestRulesVersion}") {
        exclude module: 'support-annotations'
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }
    androidTestImplementation("com.android.support.test.espresso:espresso-core:${rootProject.ext.espressoCoreVersion}") {
        exclude module: 'support-annotations'
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }
    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:${rootProject.ext.espressoContribVersion}") {
        exclude module: 'support-annotations'
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }

    implementation project(':domain')
    implementation project(':data')
    implementation project(':database')

    apply plugin: 'com.google.gms.google-services'
}
